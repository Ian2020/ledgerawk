#!/bin/awk -f
# TODO: Cope with US Bank transactions for checks - they have an extra field for the check number
BEGIN {
  FS="\t"
}
{
  if (!inTrans) {
    inTrans=1
    # Guard
    if(!match($0,"^ ?\t?[0-9][0-9]")) {
      print "Expected a date, got \"" $0 "\""
      exit 1
    }
    FS="[/]"
    $0=$0
    date = $3 "/" $1 "/" $2
    gsub("[ \t]", "", date) # Trim out all the whitespace
    FS="\t"
  } else {
    # We need to watch for the end of desc and start of transactions
    if(match($8, "^\\$")) { # A dollar sign in the 8th field
      sub("^ ", "", desc)
      # Work out if credit or debit
      if (match($6, "^\\$")) {
        amount = "-" $6
      } else {
        amount = $4
      }
      # TODO: Pick a safer separator for output?
      # TODO: Ensure width of first field is always big enough
      printf("%.4d\t%s\t%s\t%s\n", NR, date, desc, amount);

      # Reset for next transaction
      desc = ""
      inTrans = 0
    } else {
      # We just found another line of the description, add it on
      desc = desc " " $0
    }
  }
}
