#!/bin/awk -f
# TODO: Cope with US Bank transactions for checks - they have an extra field for the check number
BEGIN {
  FS="\t"
}
{
  if (!inTrans) {
    inTrans=1
    # Guard
    if(!match($0,"^ ?\t?[0-9][0-9]")) {
      print "Expected a date, got \"" $0 "\""
      exit 1
    }
    FS="[/]"
    $0=$0
    date = $3 "/" $1 "/" $2
    gsub("[ \t]", "", date) # Trim out all the whitespace
    FS="\t"
  } else {
    # We need to watch for the end of desc and start of transactions
    # At the moment we use the num fields to indicate this which might not be robust
    if(NF > 6) {
      # Num fields may be 7 or 8 on the final line
      # Checking accounts have an extra field ("check #")
      if(NF == 8) {
        deposit = $4
        withdrawal = $6
      } else {
        deposit = $3
        withdrawal = $5
      }

      # Tidy up description
      sub("^ ", "", desc)

      # Work out if credit or debit
      if (match(withdrawal, "^\\$")) {
        amount = "-" withdrawal
      } else {
        amount = deposit
      }
      gsub(" ", "", amount) # Trim out all the whitespace
      # TODO: Pick a safer separator for output?
      # TODO: Ensure width of first field is always big enough
      printf("%.4d\t%s\t%s\t%s\n", NR, date, desc, amount);

      # Reset for next transaction
      desc = ""
      inTrans = 0
    } else {
      # We just found another line of the description, add it on
      desc = desc " " $0
    }
  }
}
